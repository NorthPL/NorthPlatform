/**
 * NorthPlatform's root build script
 * Also look at Sources/build.gradle
 */

plugins {
    id 'org.ajoberstar.grgit' version '4.0.0'
    id "io.freefair.lombok" version "4.1.6"
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

allprojects {
    repositories() {
        flatDir {
            dirs "$rootDir/Libraries"
        }

        mavenLocal()

        mavenCentral()

        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.github.johnrengelman.shadow'

    jar {
        // Add some attributes to every manifest
        manifest {
            attributes 'Name': project.name
            attributes 'Implementation-Version': project.version
        }
    }

    shadowJar {
        archiveName "${project.name}.jar"
    }

    test {
        useJUnitPlatform()
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    version = grgit.head().abbreviatedId

    // execute shadowJar when we run 'gradle build'
    tasks.build.dependsOn tasks.shadowJar
}